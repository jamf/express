WHITESPACE = _{ " " | "\t" }

symbol_first_char = _{ ASCII_ALPHA }
symbol_char = _{ symbol_first_char | ASCII_DIGIT | "-" | "_" }
symbol = @{ symbol_first_char ~ symbol_char* }

raw_text = ${ ( !"{{" ~ !editable_end_tag ~ ANY )+ }

variable = !{ "{{" ~ symbol ~ "}}" }

editable_begin_tag = !{ "{{@" ~ symbol ~ "}}" ~ NEWLINE? }
editable_end_tag = !{ NEWLINE? ~ "{{@" ~ "/" ~ "}}" }

editable = ${ editable_begin_tag ~ elements ~ editable_end_tag }

element = { raw_text |
            editable |
            variable }

elements = @{ element* }

template = _{ SOI ~ elements ~ EOI }